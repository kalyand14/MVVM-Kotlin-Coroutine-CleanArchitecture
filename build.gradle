// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath ToolsDeps.gradleandroid
        classpath ToolsDeps.kotlin
        classpath ToolsDeps.gradleversions
        classpath ToolsDeps.navigation_safe_args
    }
}
allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            android {
                buildToolsVersion Versions.build_buildTools
                compileSdkVersion Versions.build_compileSdk

                defaultConfig {
                    minSdkVersion Versions.build_minSdk
                    targetSdkVersion Versions.build_targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility Versions.build_javaVersion
                    targetCompatibility Versions.build_javaVersion
                }

                android {
                    sourceSets {
                        String sharedTestDir = 'src/sharedTest/java'
                        test {
                            java.srcDirs += sharedTestDir
                        }
                        androidTest {
                            java.srcDirs += sharedTestDir
                        }
                    }
                }

                buildTypes {
                    debug {
                        minifyEnabled false
                        testCoverageEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                        testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
                    }

                    release {
                        minifyEnabled true
                        shrinkResources true
                        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                        testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
                    }
                }

                flavorDimensions "default"

                // If you need to add more flavors, consider using flavor dimensions.
                productFlavors {
                    mock {
                        dimension "default"
                        applicationIdSuffix = ".mock"
                    }
                    prod {
                        dimension "default"
                    }
                }

                // Remove mockRelease as it's not needed.
                android.variantFilter { variant ->
                    if (variant.buildType.name == 'release'
                            && variant.getFlavors().get(0).name == 'mock') {
                        variant.setIgnore(true)
                    }
                }

                // Always show the result of every unit test, even if it passes.
                testOptions.unitTests {
                    includeAndroidResources = true

                    all {
                        testLogging {
                            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                        }
                    }
                }

                kotlinOptions {
                    jvmTarget = Versions.build_javaVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}